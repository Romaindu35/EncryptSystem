plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.github.romaindu35'
archivesBaseName = "EnscryptSystem"
version '1.2.4'

repositories {
    mavenCentral()
}

dependencies {

}

/* Maven central publish code */
task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}
artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            pom {
                groupId = project.group
                artifactId = 'enscryptsystem'
                version = project.version
                name = project.name
                description = 'A library to encode/decode file and directory'
                url = 'https://github.com/Romaindu35/EncryptSystem'

                scm {
                    connection = 'scm:git:git://github.com/Romaindu35/EncryptSystem.git'
                    developerConnection = 'scm:git:ssh://github.com:Romaindu35/EncryptSystem.git'
                    url = 'https://github.com/Romaindu35/EncryptSystem/tree/master'
                }

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'Romaindu35'
                        name = "Romain SAI-TANGUY"
                        email = "romain.sai-tanguy@sfr.fr"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            def releasesRepoUrl = "https://s01.oss.sonatype.org/content/repositories/releases/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}